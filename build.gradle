plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'org.jetbrains.kotlin.jvm' version '1.2.71'
    id 'org.jetbrains.kotlin.kapt' version '1.2.71'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.2.71'
    id 'org.springframework.boot' version '2.0.6.RELEASE'
    id 'com.jfrog.bintray' version '1.8.4'
}

ext {
    kotlinVersion = '1.2.71'
    springBootVersion = '2.0.6.RELEASE'
}

allprojects {
    group = 'me.ruslanys.telegraff'
    version = '1.0.0-RC.6'

    repositories {
        jcenter()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = JavaVersion.VERSION_1_8

    dependencies {
        // Kotlin
        compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        compile("org.jetbrains.kotlin:kotlin-reflect")
        compile("org.jetbrains.kotlin:kotlin-script-util")
        compile("org.jetbrains.kotlin:kotlin-compiler-embeddable")
        compile("com.fasterxml.jackson.module:jackson-module-kotlin")

        // Tests
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("org.jetbrains.kotlin:kotlin-test")
    }

    dependencyManagement {
        imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.kotlin.srcDirs += 'src/main/kotlin'
        test.kotlin.srcDirs += 'src/test/kotlin'
    }

    // Jacoco
    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
    check.dependsOn jacocoTestReport

    // IDEA
    idea {
        module {
            def kaptMain = file('build/generated/source/kapt/main')
            sourceDirs += kaptMain
            generatedSourceDirs += kaptMain
        }
    }

    // Kotlin
    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    // Jar
    jar {
        enabled = true
        manifest {
            attributes("Implementation-Version": version)
        }
        archiveName = rootProject.name + ".jar"
    }

    bootJar {
        enabled = false
    }

    bintray {
        user = System.getenv("BINTRAY_USER") ?: System.properties['bintrayUser']
        key = System.getenv("BINTRAY_KEY") ?: System.properties['bintrayKey']
        configurations = ['archives']

        pkg {
            repo = "maven"
            name = project.name
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/ruslanys/telegraff.git'
        }
    }

}

bootJar {
    enabled = false
}